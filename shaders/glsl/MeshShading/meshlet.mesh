#version 450
#extension GL_EXT_scalar_block_layout : require
#extension GL_EXT_shader_16bit_storage: require
#extension GL_EXT_shader_8bit_storage: require
#extension GL_EXT_mesh_shader: require

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;
layout(triangles, max_vertices = 64, max_primitives = 126) out;

layout(set = 0, binding = 0) uniform ubo_data
{
	mat4 view;
	mat4 projection;
};

struct Vertex
{
    vec3 pos;
    vec3 color;
    vec2 uv;
};

layout(binding = 1, scalar) readonly buffer Vertices
{
    Vertex vertices[];
};

struct Meshlet
{
    uint data_offset;
    uint vertex_offset;
    uint8_t triangleCount;
    uint8_t vertexCount;
};

layout(binding = 2) readonly buffer Meshlets
{
    Meshlet meshlets[];
};

layout(binding = 3) readonly buffer MeshletDataBuffer
{
    uint meshlet_data[];
};

vec3 randomColor(uint seed)
{
    seed = (seed ^ 61u) ^ (seed >> 16u);
    seed *= 9u;
    seed = seed ^ (seed >> 4u);
    seed *= 0x27d4eb2du;
    seed = seed ^ (seed >> 15u);

    float r = float((seed & 0xFFu)) / 255.0;
    float g = float((seed >> 8u) & 0xFFu) / 255.0;
    float b = float((seed >> 16u) & 0xFFu) / 255.0;

    return vec3(r, g, b);
}

layout(location = 0) out vec4 color[];

void main()
{
    uint mi = gl_WorkGroupID.x;
    uint ti = gl_LocalInvocationID.x;
	uint vertexCount = uint(meshlets[mi].vertexCount);
    uint indexCount = uint(meshlets[mi].triangleCount) * 3;
    uint primCount = uint(meshlets[mi].triangleCount);

    SetMeshOutputsEXT(vertexCount, primCount);

    vec3 meshletColor = randomColor(mi);

    for (uint i = ti; i < uint(meshlets[mi].vertexCount); i += 32)
    {
        uint vi = meshlet_data[meshlets[mi].data_offset + i] + meshlets[mi].vertex_offset;

        vec3 position = vec3(vertices[vi].pos);
        vec2 texcoord = vec2(vertices[vi].uv);

        gl_MeshVerticesEXT[i].gl_Position = projection * view * vec4(position, 1.0);
        color[i] = vec4(meshletColor, 1.0);
    }

    uint base = meshlets[mi].data_offset + vertexCount;

    for (uint tri = ti; tri < primCount; tri += 32)
    {
        // calculate index byte by 4 bytes
        uint i0 = meshlet_data[base + (tri * 3 + 0) / 4];
        uint i1 = meshlet_data[base + (tri * 3 + 1) / 4];
        uint i2 = meshlet_data[base + (tri * 3 + 2) / 4];

        // calculate mask by 4 bytes (multiple with 8 to get the correct bit position)
        uint s0 = ((tri * 3 + 0) % 4) * 8;
        uint s1 = ((tri * 3 + 1) % 4) * 8;
        uint s2 = ((tri * 3 + 2) % 4) * 8;

        // calculate index byte by mask
        uint v0 = (i0 >> s0) & 0xFF;
        uint v1 = (i1 >> s1) & 0xFF;
        uint v2 = (i2 >> s2) & 0xFF;

        gl_PrimitiveTriangleIndicesEXT[tri] = uvec3(v0, v1, v2);
    }
}